enum ServerState {
	UNINITIALIZED,
	RUNNING,
	SHUTDOWN,
	EXIT
}

// The main class for handling LSP lifecycle.
class Server {

	def log = Logger.new("server");
	def config = ServerConfig.new();
	def comm = Channel.new(0);
	def subscriber = EventBusSubscriber<IncomingData>.new();

	var state = ServerState.UNINITIALIZED;
	var exit_code = 0;

	def register_packet_subscriber = subscriber.register;

	new() {
		subscriber.filter = filterEvent;
		register_packet_subscriber("initialize", initialize);
		register_packet_subscriber("shutdown", shutdown);
		register_packet_subscriber("exit", exit);
	}

	def initialize(packet: IncomingData) {
		var p = InitializePacket.!(packet);
		config.root_path = p.root_path;
		state = ServerState.RUNNING;
		if (Log.server) log.put1("Server started on workspace %s.", config.root_path);

		var capabilities = ServerCapabilities.new();
		var server_info = ServerInfo.new("Virgil Server :3", "0.1.0");
		comm.send(ResponseMessage.Data(InitializeResult.new(capabilities, server_info)));
	}

	def shutdown(packet: IncomingData) {
		if (Log.server) log.puts("Server received SHUTDOWN.");
		state = ServerState.SHUTDOWN;
		comm.send(ResponseMessage.Data(ShutdownResult.new()));
	}

	def exit(packet: IncomingData) {
		if (Log.server) log.puts("Server received EXIT.");
		if (state != ServerState.SHUTDOWN) {
			Log.err.puts("Server did not receive SHUTDOWN before EXIT.");
			exit_code = 1;
		}
		state = ServerState.EXIT;
	}

	def loop() -> int {
		while (state != ServerState.EXIT) {
			var packet = comm.read();
			match (packet) {
				RPCError(e) => comm.sendSimpleError(e);
				Success(method, data) => subscriber.fire(method, data);
			}
		}
		log.puts("Exiting server main loop.");
		return exit_code;
	}

	// Used to disable the event bus based on the current {Server.state}.
	// e.g., if any packet other than "initialize" is received when the server is
	// uninitialized, cancel the event and send a {SERVER_NOT_INITIALIZED} error.
	private def filterEvent(method: string, packet: IncomingData) -> bool {
		if (state == ServerState.UNINITIALIZED && !Strings.equal(method, "initialize")) {
			comm.sendSimpleError(ErrorCodes.SERVER_NOT_INITIALIZED);
			return false;
		}

		if (state == ServerState.SHUTDOWN && !Strings.equal(method, "exit")) {
			comm.sendSimpleError(ErrorCodes.INVALID_REQUEST);
			return false;
		}

		return true;
	}
}

component LSPManager {
	var server: Server;

	def createServer() -> Server {
		if (server != null) {
			Log.err.puts("Unable to start server as an instance is already running.");
			return null;
		}
		server = Server.new();
		return server;
	}
}

class ServerConfig {
	var root_path: string;
}
