enum SessionState {
	UNINITIALIZED,
	RUNNING,
	SHUTDOWN,
	EXIT
}

// The main class for handling LSP lifecycle.
class LSPSession(config: ServerConfig, conn: IOStream<byte>) {

	def log = Logger.new("session");
	def comm = Communication.new(conn);
	def subscriber = EventBusSubscriber<IncomingData>.new();

	var state = SessionState.UNINITIALIZED;
	var exit_code = 0;

	def register_packet_subscriber = subscriber.register;

	new() {
		register_packet_subscriber("initialize", initialize);
		register_packet_subscriber("shutdown", shutdown);
		register_packet_subscriber("exit", exit);
	}

	def initialize(packet: IncomingData) {
		state = SessionState.RUNNING;
		if (Log.session) log.puts("Session started.");
	}

	def shutdown(packet: IncomingData) {
		state = SessionState.SHUTDOWN;
		if (Log.session) log.puts("Session received SHUTDOWN.");
	}

	def exit(packet: IncomingData) {
		if (Log.session) log.puts("Session received EXIT.");
		if (state != SessionState.SHUTDOWN) {
			Log.err.puts("Session did not receive SHUTDOWN before EXIT.");
			exit_code = 1;
		}
		state = SessionState.EXIT;
	}

	def loop() -> int {
		while (state != SessionState.EXIT) {
			var packet = comm.read();
			match (packet) {
				RPCError(e) => comm.sendSimpleError(e);
				Success(method, data) => {
					if (state == SessionState.UNINITIALIZED && !Strings.equal(method, "initialize")) {
						comm.sendSimpleError(ErrorCodes.SERVER_NOT_INITIALIZED);
						continue;
					}
					if (state == SessionState.SHUTDOWN && !Strings.equal(method, "exit")) {
						comm.sendSimpleError(ErrorCodes.INVALID_REQUEST);
						continue;
					}
					subscriber.fire(method, data);
				}
			}
		}
		log.puts("Exiting session main loop.");
		return exit_code;
	}
}

class ServerConfig {
	var log_path: string;

	def parseOptions(args: Range<string>) {
		for (i < args.length) {
			if (Strings.equal(args[i], "--log") && i + 1 < args.length) {
				log_path = args[i + 1];
				LogManager.initLogging(log_path);
			}
		}
	}
}
